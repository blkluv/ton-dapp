(int, slice, slice, slice, cell, cell) load_data() inline_ref {
    slice data = get_data().begin_parse();

    ;; 0 - Not inited
    ;; 1 - Inited, ready for accept coins | new configuration
    ;; 2 - Closed, NFT was bought
    ;; 3 - Closed, seller was canceled
    int current_mode = data~load_uint(8);

    if (current_mode == 0) {
        ;; While uninicialize need to skip unixtime
        data~skip_bits(32);
    }

    ;; NFT which we want to sell
    slice nft_address = data~load_msg_addr();
    slice deployer = data~load_msg_addr();
    slice buyer = data~load_msg_addr();
    cell sell_config = data~load_ref();
    cell price_config = data~load_ref();
    data.end_parse();

    return (current_mode,
            nft_address,
            deployer,
            buyer,
            sell_config,
            price_config);
}

() store_data(int current_mode,
        slice nft_address,
        slice deployer,
        slice buyer,
        cell sell_config,
        cell price_config) impure inline_ref {

    builder b = begin_cell();
    b = store_uint(b, current_mode, 8);
    b = store_slice(b, nft_address);
    b = store_slice(b, deployer);
    b = store_slice(b, buyer);
    b = store_ref(b, sell_config);
    b = store_ref(b, price_config);

    set_data(b.end_cell());
}


(int, int) parse_price_config(cell price_config){
    slice price_config_cs = price_config.begin_parse();
    int is_ton = price_config_cs~load_uint(1);
    int price = price_config_cs~load_grams();
    price_config_cs.end_parse();

    return (is_ton, price);
}

(int, int, int, int, slice) parse_sell_config(cell sell_config){
    slice sell_config_cs = sell_config.begin_parse();

    int marketplace_numerator = sell_config_cs~load_uint(16);
    int marketplace_denominator = sell_config_cs~load_uint(16);

    int royalty_numerator = sell_config_cs~load_uint(16);
    int royalty_denominator = sell_config_cs~load_uint(16);

    slice royalty_destanation = sell_config_cs~load_msg_addr();
    sell_config_cs.end_parse();

    return (marketplace_numerator,
            marketplace_denominator,
            royalty_numerator,
            royalty_denominator,
            royalty_destanation);
}